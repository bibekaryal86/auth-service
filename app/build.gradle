plugins {
    id 'application'
    id 'java'
    id 'io.freefair.lombok' version '8.6'
    id 'org.springframework.boot' version '3.3.1'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.flywaydb.flyway' version '10.15.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = 'user-mgmt-sys.jar'
}

dependencies {
    def springVersion = "3.3.1"
    def junitVersion = "5.10.2"
    
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-web:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-security:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-cache:$springVersion"

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    
    // FOR ClassNotFoundException javax.xml.bind.JAXBException
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    // FOR TESTING
    testImplementation("org.springframework.boot:spring-boot-starter-test:$springVersion") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

application {
    mainClass = 'user.management.system.App'
}

configurations {
    implementation.exclude module: 'spring-boot-starter-logging'
    implementation.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
}

spotless {
    java {
        removeUnusedImports()
        googleJavaFormat()
    }
}

flyway {
    url = 'jdbc:postgresql://stalwartly-sincere-mongoose.data-1.use1.tembo.io:5432/postgres'
    // user = 'myUsr'
    // password = 'mySecretPwd'
    // username and password provided as environment variables
}
